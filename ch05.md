# 组件

标签（空格分隔）： Yesod-Book

---

web 开发的困难之一，我们必须配合三种客户端技术：HTML，CSS，Javascript。更糟糕的是，我们必须把它放在网页的不同位置：CSS在head中一个style标签，Javascript在body标签前script标签中，HTML在body中。如果你想要把你的CSS和Javascript放在在单独的文件中，你不用担心这些！

在实践中，创建一个单页面，这种方式能够工作得很好，因为我们可以分离我们的结构（HTML），样式（CSS），逻辑（Javascript）。但是当我们想将其模块化以至于能够更容易的组合，协调这三块内容就会变得非常头疼。组件就是Yesod对这种问题的解决方案。这样对于防止重复包含库问题，也能很好的解决。

我们四种模板语言-Hamlet，Cassius，Lucius，Julius，提供了原始的工具来构建你的输出。组件提供了粘合剂来让它们之间无缝衔接。

## 概要

```haskell
{-# LANGUAGE OverloadedStrings #-}
{-# LANGUAGE QuasiQuotes       #-}
{-# LANGUAGE TemplateHaskell   #-}
{-# LANGUAGE TypeFamilies      #-}
import           Yesod

data App = App
mkYesod "App" [parseRoutes|
/ HomeR GET
|]
instance Yesod App

getHomeR = defaultLayout $ do
    setTitle "My Page Title"
    toWidget [lucius| h1 { color: green; } |]
    addScriptRemote "https://ajax.googleapis.com/ajax/libs/jquery/1.6.2/jquery.min.js"
    toWidget
        [julius|
            $(function() {
                $("h1").click(function(){
                    alert("You clicked on the heading!");
                });
            });
        |]
    toWidgetHead
        [hamlet|
            <meta name=keywords content="some sample keywords">
        |]
    toWidget
        [hamlet|
            <h1>Here's one way of including content
        |]
    [whamlet|<h2>Here's another |]
    toWidgetBody
        [julius|
            alert("This is included in the body itself");
        |]

main = warp 3000 App
```
会产生如下HTML（已经加入缩进）

```haskell
<!DOCTYPE html>
<html>
  <head>
    <title>My Page Title</title>
    <meta name="keywords" content="some sample keywords">
    <style>h1{color:green}</style>
  </head>
  <body>
    <h1>Here's one way of including content</h1>
    <h2>Here's another</h2>
    <script>
      alert("This is included in the body itself");
    </script>
    <script src="https://ajax.googleapis.com/ajax/libs/jquery/1.6.2/jquery.min.js">
    </script><script>
      $(function() {
        $('h1').click(function() {
          alert("You clicked on the heading!");
        });
      });
    </script>
  </body>
</html>
```





